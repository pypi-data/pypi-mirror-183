0010 !  MY&IP7 - 7TH(LAST) PART IPBI
0020        BYT  241
0030 SCRATB BSZ  0
0040        LDB  R22,=2           ! SET ROMFL TO 'SCRATCH'
0050        STBD R22,=ROMFL 
0060        JSB  =BPINI           ! LET BPGM INITIALIZE TO RELEASE HOOK TAKEN
0061 GOTOSU DAD  17435
0062 RSMEM- DAD  37453
0063 PLOTSY DAD  100116
0064 R6LIM2 DAD  101720
0065 SETTR1 DAD  2323
0066 CLRBIT DAD  546
0070 BPINI  DAD  4706
0080        BIN  
0090        STBD R#,=GINTDS       ! DISABLE INTERRUPTS
0100        LDMD R24,=BINTAB      ! LOAD BASE ADDRESS
0110        DCM  R24              ! MOVE TO LAST BYTE TO KEEP
0120        LDMD R26,=LWAMEM      ! GET END OF MEMORY (AND BPGM)
0130        STM  R26,R22          ! COPY
0140        SBM  R22,R24          ! GET DISTANCE TO MOVE
0150        LDB  R20,=4           ! LOAD COUNTER FOR POINTER ADJUST
0160        LDM  R32,=LAVAIL      ! GET ADDRESS OF 1st POINTER TO MOVE
0170 UNLD1  LDMD R36,R32          ! GET NEXT POINTER
0180        ADM  R36,R22          ! ADD DISTANCE TO MOVE
0190        PUMD R36,+R32         ! RESTORE POINTER
0200        DCB  R20              ! DECREMENT COUNT
0210        JNZ  UNLD1            ! JIF NOT DONE
0220        LDMD R36,R32          ! GET FWBIN
0230        CMMD R36,=LWAMEM      ! SAME AS LWAMEM?
0240        JZR  UNLD2            ! JIF YES
0250        ADM  R36,R22          ! ELSE ADJUST
0260        STMD R36,R32          !   AND REPLACE
0270 UNLD2  CLM  R#
0280        STMD R#,=BINTAB       ! ZERO OUT BINTAB (NO BPGM)
0290        LDM  R#,R12           ! COPY R12POINTER
0300        LDM  R41,=316         ! LOAD INTO R41-R47 THIS CODE:
0310        DEF  MOVDN            !             JSB=MOVDN
0320        STBD R#,=GINTEN       !             STBD R#,=GINTEN
0330        RTN                   !             RTN
0340        STMD R41,R36          ! STORE AT END OF R12 STACK
0350        DCM  R36              ! DCM ADDR. BECAUSE LDM WILL ICM R4 AFTER LOAD
0360        LDM  R4,R36           ! MOVE PROGRAM EXECUTION TO MOVDN CODE
0370        GLO  GLOB25
0380        FIN  
